ad_library {
    Library to generate and process form elements for Connections

    @author Hamilton Chua (ham@soluiongrove.com)
    @creation-date 2006-01-11
}

namespace eval connections::form { }

ad_proc -public connections::form::generate  {
	-form_id:required
	-object_id:required
	{-num_of_con "20"}
} {
	Generate an extended ad_form with textboxes for users to enter connection keywords
	associated with the given object_id. If the given object_id has any existing keywords associated with it, 
	alter the num_of_connections to the number of existing keywords.

	@author Hamilton Chua
	@param form_id The form id or name of an existing form.
	@param object_id The object_id that you want to map keywords to.
	@param num_of_con The number of textboxes to generate.
} {
	set existing_con_list [connections::getlist -object_id $object_id -with_url "0"]
	set existing_num_of_con [llength $existing_con_list]

	if { $existing_num_of_con > $num_of_con } {
		set num_of_con $existing_num_of_con
	}

	# create elements
	for { set x 0 } { $x < $num_of_con } { incr x } {
		template::element::create $form_id conkeyword${x} -datatype "text" -optional -label "" -section "Tags"
	}

	# set elements
	if { [exists_and_not_null object_id] } {
		for { set x 0 } { $x < $num_of_con } { incr x } {
			if { [lindex $existing_con_list $x] != "" } {
				template::element::set_value $form_id conkeyword${x} [lindex $existing_con_list $x]
			}
		}	
	}
}

ad_proc -public connections::form::process {
	-form_id:required
	-object_id:required
	{-num_of_con "20"}
} {
	Retrieves field values generated by connections::form::generate and saves the connection keywords.

	@author Hamilton Chua
	@param form_id The form id or name of an existing form.
	@param object_id The object_id that you want to map keywords to.
	@param num_of_con The number of text fields from which you will get connection keywords..

} {
	connections::remove_all -object_id $object_id

	for { set x 0 } { $x < $num_of_con } { incr x } {
		if { [template::element::get_value $form_id conkeyword${x}] != "" } {
			connections::create -object_id $object_id -keyword [template::element::get_value $form_id conkeyword${x}]
		}
	}	
}